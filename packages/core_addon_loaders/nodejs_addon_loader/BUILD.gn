#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

action("copy_node_shared_lib") {
  script = "//.gnfiles/build/scripts/copy_fs_entry.py"

  args = [
    "--source",
    rebase_path("${root_out_dir}/prebuilt/node_shared/lib/libnode.so"),
    "--destination",
    rebase_path(
        "${root_out_dir}/ten_packages/addon_loader/nodejs_addon_loader/lib/libnode.so"),
  ]

  outputs = [ "${root_out_dir}/ten_packages/addon_loader/nodejs_addon_loader/lib/libnode.so" ]

  deps = [ "//third_party/node" ]
}

config("nodejs_addon_loader_config") {
  cflags_cc = [
    "-std=c++17",
    "-stdlib=libstdc++",
  ]
}

ten_package("nodejs_addon_loader") {
  package_kind = "addon_loader"
  enable_build = true

  resources = [
    "manifest.json",
    "property.json",
  ]

  configs = [ ":nodejs_addon_loader_config" ]

  sources = [ "src/main.cc" ]
  include_dirs = [ "//core" ]

  deps = [
    ":copy_node_shared_lib",
    "//core/src/ten_runtime",
    "//third_party/node",
  ]

  lib_dirs =
      [ "${root_out_dir}/ten_packages/addon_loader/nodejs_addon_loader/lib" ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_nodejs_addon_loader_to_server") {
    base_dir = rebase_path(
            "${root_out_dir}/ten_packages/addon_loader/nodejs_addon_loader")
    deps = [ ":nodejs_addon_loader" ]
  }
}
