#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")
import("//third_party/nlohmann_json/output.gni")

copy_nlohmann_headers("copy_nlohmann_headers_for_default_extension_cpp") {
  dest_dir = rebase_path(
          "${root_out_dir}/ten_packages/extension/default_extension_cpp/include")
}

ten_package("default_extension_cpp") {
  package_kind = "extension"

  resources = [
    "BUILD_release.gn.tent=>BUILD.gn.tent",
    "BUILD_release.gn=>BUILD.gn",
    "manifest.json",
    "manifest.json.tent",
    "property.json",
    "src/main.cc",
    "src/main.cc.tent",
  ]

  tests_files = exec_script("//build/ten_common/general/glob_file.py",
                            [
                              "--dir",
                              rebase_path("tests/**/*"),
                              "--dir-base",
                              rebase_path("tests"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(tests_file, tests_files) {
    tests_file_rel_path = tests_file.relative_path
    resources +=
        [ "tests/${tests_file_rel_path}=>tests/${tests_file_rel_path}" ]
  }

  deps = [ ":copy_nlohmann_headers_for_default_extension_cpp" ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_default_extension_cpp_to_server") {
    base_dir = rebase_path(
            "${root_out_dir}/ten_packages/extension/default_extension_cpp")
    deps = [ ":default_extension_cpp" ]
  }
}
