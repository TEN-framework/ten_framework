#
# This file is part of the TEN Framework project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_package("py_init_extension_cpp") {
  package_kind = "extension"
  enable_build = true

  resources = [
    "manifest.json",
    "property.json",

    # We want to pack the source code even in the 'release' package, then users
    # could choose to use this extension directly as a 'release' package, or
    # develop their own extension based on this.
    "src",
  ]

  sources = [ "src/main.cc" ]
  include_dirs = [ "//core" ]

  ldflags = []
  if (is_mac) {
    ldflags +=
        [ "-Wl,-rpath,@loader_path/../../../system/ten_runtime_python/lib" ]
  } else if (is_linux) {
    ldflags += [
      "-Wl,-rpath=\$ORIGIN/../../../system/ten_runtime_python/lib",
      "-Wl,-rpath-link=\$ORIGIN/../../../system/ten_runtime_python/lib",
    ]
  }

  deps = [
    "//core/src/ten_runtime",
    "//core/src/ten_runtime/binding/python",
  ]
}

if (ten_enable_package_manager) {
  ten_package_publish("upload_py_init_extension_cpp_to_server") {
    base_dir = rebase_path(
            "${root_out_dir}/ten_packages/extension/py_init_extension_cpp")
    deps = [ ":py_init_extension_cpp" ]
  }
}
