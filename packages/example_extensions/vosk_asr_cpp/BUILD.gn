#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/feature/ten_package_test.gni")
import("//build/options.gni")
import("//build/ten_runtime/feature/publish.gni")

ten_package("vosk_asr_cpp") {
  package_kind = "extension"

  resources = [
    "BUILD_release.gn=>BUILD.gn",
    "LICENSE",
    "README.md",
    "manifest.json",
    "property.json",
    "src/main.cc",
  ]

  header_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                             [
                               "--dir",
                               rebase_path("include/**/*"),
                               "--dir-base",
                               rebase_path("include"),
                               "--recursive",
                               "--only-output-file",
                             ],
                             "json")

  foreach(header_file, header_files) {
    header_file_rel_path = header_file.relative_path
    resources +=
        [ "include/${header_file_rel_path}=>include/${header_file_rel_path}" ]
  }

  tests_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("tests/**/*"),
                              "--dir-base",
                              rebase_path("tests"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(tests_file, tests_files) {
    tests_file_rel_path = tests_file.relative_path
    resources +=
        [ "tests/${tests_file_rel_path}=>tests/${tests_file_rel_path}" ]
  }
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_vosk_asr_cpp_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/ten_packages/extension/vosk_asr_cpp")
    deps = [ ":vosk_asr_cpp" ]
  }
}
