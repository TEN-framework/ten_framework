#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/ten_package.gni")
import("//build/options.gni")
import("//build/ten_runtime/feature/javascript.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/options.gni")
import("//build/ten_runtime/ten.gni")
import("//core/src/ten_runtime/output_libs.gni")

nodejs_library("ten_runtime_nodejs_js") {
  tsconfig = "tsconfig.json"
  package_json = "package.json"
  output_dir = "$root_out_dir/ten_packages/system/ten_runtime_nodejs"
  remove_node_modules = true
  remove_tsbuildinfo = true
  remove_src = true
}

ten_package("ten_nodejs_binding_system_package") {
  package_kind = "system"
  package_output_root_dir_name = "ten_runtime_nodejs"

  resources = [
    "manifest.json",
    "package.json",
    "tsconfig.json",
  ]

  interface_files = exec_script(
          "//.gnfiles/build/scripts/glob_file.py",
          [
            "--dir",
            rebase_path("//core/src/ten_runtime/binding/nodejs/interface/**/*"),
            "--dir-base",
            rebase_path("//core/src/ten_runtime/binding/nodejs/interface/"),
            "--recursive",
            "--only-output-file",
          ],
          "json")

  foreach(interface_file, interface_files) {
    interface_file_rel_path = interface_file.relative_path
    resources += [ "interface/${interface_file_rel_path}=>interface/${interface_file_rel_path}" ]
  }

  foreach(lib, ten_runtime_nodejs_output_libs) {
    libname = get_path_info(rebase_path(lib), "file")
    resources += [ "${lib}=>lib/${libname}" ]
  }

  deps = [
    ":ten_runtime_nodejs_js",
    "native:ten_runtime_nodejs",
  ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_ten_nodejs_binding_system_package_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/ten_packages/system/ten_runtime_nodejs")
    deps = [ ":ten_nodejs_binding_system_package" ]
  }
}

group("nodejs") {
  deps = [
    ":ten_runtime_nodejs_js",
    "native:ten_runtime_nodejs",
  ]

  if (ten_enable_ten_manager) {
    deps += [ ":upload_ten_nodejs_binding_system_package_to_server" ]
  }
}
