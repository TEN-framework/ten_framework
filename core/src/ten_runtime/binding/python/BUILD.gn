#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/ten_package.gni")
import("//build/options.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/options.gni")
import("//build/ten_runtime/ten.gni")
import("//core/src/ten_runtime/output_libs.gni")

ten_package("ten_python_binding_system_package") {
  package_kind = "system"
  package_output_root_dir_name = "ten_runtime_python"

  resources = [ "manifest.json" ]

  interface_files = exec_script(
          "//build/ten_common/general/glob_file.py",
          [
            "--dir",
            rebase_path("//core/src/ten_runtime/binding/python/interface/**/*"),
            "--dir-base",
            rebase_path("//core/src/ten_runtime/binding/python/interface/"),
            "--recursive",
            "--only-output-file",
          ],
          "json")

  foreach(interface_file, interface_files) {
    interface_file_rel_path = interface_file.relative_path
    resources += [ "interface/${interface_file_rel_path}=>interface/${interface_file_rel_path}" ]
  }

  tools_files = exec_script(
          "//build/ten_common/general/glob_file.py",
          [
            "--dir",
            rebase_path("//core/src/ten_runtime/binding/python/tools/**/*"),
            "--dir-base",
            rebase_path("//core/src/ten_runtime/binding/python/tools/"),
            "--recursive",
            "--only-output-file",
          ],
          "json")

  foreach(tool_file, tools_files) {
    tool_file_rel_path = tool_file.relative_path
    resources += [ "tools/${tool_file_rel_path}=>tools/${tool_file_rel_path}" ]
  }

  foreach(lib, ten_runtime_python_output_libs) {
    libname = get_path_info(rebase_path(lib), "file")
    resources += [ "${lib}=>lib/${libname}" ]
  }

  deps = [ "native:ten_runtime_python" ]
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_ten_python_binding_system_package_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/ten_packages/system/ten_runtime_python")
    deps = [
      ":ten_python_binding_system_package",
      "native:ten_runtime_python",
    ]
  }
}

group("python") {
  deps = [ "native:ten_runtime_python" ]

  if (ten_enable_ten_manager) {
    deps += [ ":upload_ten_python_binding_system_package_to_server" ]
  }
}
