#
# This file is part of the TEN Framework project.
# See https://github.com/TEN-framework/ten_framework/LICENSE for license
# information.
#
import("//build/options.gni")
import("//build/ten_runtime/ten.gni")

ten_shared_library("ten_runtime_go") {
  if (is_mac || is_linux) {
    add_configs = [ "//.gnfiles/build/toolchain/common:allow_undefined" ]
    remove_configs = [ "//.gnfiles/build/toolchain/common:disallow_undefined" ]
  }

  sources = [ "//core/src/ten_runtime/binding/go/native/build_template/preserved_metadata.c" ]

  include_dirs = ten_runtime_common_includes
  include_dirs += [
    "//src",
    "${root_out_dir}/obj",
    "${root_gen_dir}/cmake/jansson/include",
  ]

  cflags = []
  if (!is_win) {
    cflags += [ "-fPIC" ]
  }

  if (!defined(ldflags)) {
    ldflags = []
  }

  # Add rpath to find ten_runtime library.
  if (is_mac) {
    ldflags += [ "-Wl,-rpath,@loader_path/../../ten_runtime/lib" ]
  } else if (is_linux) {
    ldflags += [ "-Wl,-rpath=\$ORIGIN/../../ten_runtime/lib" ]
  }

  # Only export "ten" relevant symbols.
  if (is_mac) {
    ldflags += [
      "-Xlinker",
      "-exported_symbols_list",
      "-Xlinker",
      rebase_path("//build/ten_runtime/ld_script/mac"),
    ]
  } else if (is_linux) {
    ldflags += [ "-Wl,--version-script=" +
                 rebase_path("//build/ten_runtime/ld_script/linux") ]
  }

  deps = [
    "addon",
    "app",
    "extension",
    "extension_group",
    "internal",
    "msg",
    "ten_env",
    "value",
    "//core/src/ten_runtime:ten_runtime_library",
  ]
}
