#
# This file is part of the TEN Framework project.
# See https://github.com/TEN-framework/ten_framework/LICENSE for license
# information.
#
import("//build/common/rust/rust.gni")
import("//build/feature/ten_package.gni")
import("//third_party/clingo/output_libs.gni")

declare_args() {
  use_shared_lib = false
}

if (ten_enable_package_manager) {
  if (is_linux || is_win || (is_mac && target_cpu == "x64")) {
    rust_test("tman_test") {
      project_path = "//core/src/ten_manager"

      clingo_lib_folder =
          rebase_path("${root_out_dir}/gen/cmake/clingo/install/lib")
      env = [ "CLINGO_LIBRARY_PATH=${clingo_lib_folder}" ]

      # The transitive dependency of the ten_rust crate.
      utils_static_lib_path = rebase_path("${root_gen_dir}/core/src/ten_utils")
      env += [ "TEN_UTILS_LIBRARY_PATH=${utils_static_lib_path}" ]

      test_output_dir = "${root_out_dir}/tests/standalone"
      through_real_test = true

      deps = [
        "//core/src/ten_rust:ten_rust_static_lib",
        "//third_party/clingo",
      ]
    }
  }

  action("copy_clingo_config_files") {
    script = "//build/common/scripts/copy_files.py"

    sources = [
      "//core/src/ten_manager/src/solver/display.lp",
      "//core/src/ten_manager/src/solver/main.lp",
    ]
    outputs = [
      "${root_out_dir}/ten_manager/solver/display.lp",
      "${root_out_dir}/ten_manager/solver/main.lp",
    ]

    args = []
    foreach(f, sources + outputs) {
      args += [ rebase_path(f) ]
    }
  }

  rust_target("tman") {
    project_path = "//core/src/ten_manager"
    manifest_path = "//core/src/ten_manager/Cargo.toml"

    clingo_lib_folder =
        rebase_path("${root_out_dir}/gen/cmake/clingo/install/lib")
    env = [ "CLINGO_LIBRARY_PATH=${clingo_lib_folder}" ]

    # The transitive dependency of the ten_rust crate.
    utils_static_lib_path = rebase_path("${root_gen_dir}/core/src/ten_utils")
    env += [ "TEN_UTILS_LIBRARY_PATH=${utils_static_lib_path}" ]

    extra_sources = [
      "//core/src/ten_manager/src/solver/display.lp",
      "//core/src/ten_manager/src/solver/main.lp",
    ]

    if (is_win) {
      output_name = "tman.exe"
    } else {
      output_name = "tman"
    }

    deps = [
      "//core/src/ten_rust:ten_rust_static_lib",
      "//third_party/clingo",
    ]
  }

  # Assembling the final ten manager package.
  ten_package("ten_manager") {
    package_kind = "custom"

    tman_output = get_target_outputs(":tman")

    if (is_win) {
      use_shared_lib = true
    }

    resources = []
    if (is_win) {
      resources += [ "${tman_output[0]}=>bin/tman.exe" ]
    } else {
      resources += [ "${tman_output[0]}=>bin/tman" ]
    }

    if (use_shared_lib) {
      lib_dest_folder = "lib"
      if (is_win) {
        # In Windows, all .dll used by the executable must be in the same folder.
        lib_dest_folder = "bin"
      }

      foreach(lib, clingo_output_libs) {
        libname = get_path_info(rebase_path(lib), "file")
        resources += [ "${lib}=>${lib_dest_folder}/${libname}" ]
      }
    }

    deps = [
      ":tman",
      "//third_party/clingo",
    ]

    if (use_shared_lib) {
      # Copy relevant resources files.
      deps += [ ":copy_clingo_config_files" ]
    }
  }
}
