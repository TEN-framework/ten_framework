#
# Copyright Â© 2024 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to https://github.com/TEN-framework/ten_framework/LICENSE for more
# information.
#
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/ten.gni")

glob("ten_runtime_smoke_tests") {
  file_list = all_native_files

  public_deps = [
    "//core/src/ten_utils",
    "//third_party/googlemock",
    "//third_party/googletest",
  ]

  include_dirs = [ "//core/include" ]
}

# TEN runtime requires an app's manifest.json to determine which parent folder
# is the app folder.
action("copy_fake_manifest_json_for_smoke_test") {
  script = "//.gnfiles/build/scripts/copy_fs_entry.py"

  inputs = [ "//tests/ten_runtime/smoke/fake_manifest.json" ]
  outputs = [ "${root_out_dir}/tests/standalone/manifest.json" ]

  args = [
    "--source",
    rebase_path(inputs[0]),
    "--destination",
    rebase_path(outputs[0]),
  ]
}

ten_executable("ten_runtime_smoke_test") {
  output_dir = "${root_out_dir}/tests/standalone"

  deps = [
    ":copy_fake_manifest_json_for_smoke_test",
    ":ten_runtime_smoke_tests",
    "//core/src/ten_runtime",
    "//core/src/ten_utils:ten_utils_shared",
    "//packages/core_extension_groups/default_extension_group:default_extension_group_files",
    "//packages/core_protocols/msgpack:msgpack_files",
    "//tests/ten_runtime:copy_libraries_for_test",
    "//third_party/nlohmann_json",
  ]

  deps += [
    "audio_frame_test",
    "cmd_conversion",
    "cmd_result_test",
    "data_test",
    "extension_test",
    "go_standalone_test",
    "graph_test",
    "interface_test",
    "msg_test",
    "notify_test",
    "result_conversion",
    "schema_test",
    "standalone_test",
    "video_frame_test",
  ]

  if (ten_enable_curl) {
    deps += [ "//third_party/curl" ]
  }

  if (ten_enable_libwebsockets) {
    deps += [
      "//third_party/libwebsockets",
      "//third_party/mbedtls",
    ]
  }

  sources = [ "//third_party/googletest/src/gtest_main.cc" ]

  public_deps = [
    "//third_party/googlemock",
    "//third_party/googletest",
  ]
}

group("smoke") {
  public_deps = [ ":ten_runtime_smoke_test" ]
}
