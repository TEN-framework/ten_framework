#
# Copyright Â© 2024 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/options.gni")

if (ten_enable_go_binding) {
  action("ten_go_standalone_test_build") {
    script = rebase_path("//build/ten_runtime/feature/go_test_build.py")

    args = [
      "--source-dir",
      rebase_path("//tests/ten_runtime/smoke/go_standalone_test"),
      "--system-go-dir",
      rebase_path("${root_out_dir}"),
      "--output",
      rebase_path("${root_out_dir}/tests/standalone/go_standalone_test"),
    ]

    if (is_clang) {
      args += [ "--is-clang" ]
    } else {
      args += [ "--no-is-clang" ]
    }

    if (enable_sanitizer) {
      args += [ "--enable-sanitizer" ]
    } else {
      args += [ "--no-enable-sanitizer" ]
    }

    inputs = [
      "basic_extension.go",
      "basic_extension_test.go",
      "cmd_test.go",
    ]

    deps = [ "//core/src/ten_runtime/binding/go" ]

    outputs = [ "${root_out_dir}/tests/standalone/go_standalone_test" ]
  }
}

group("go_standalone_test") {
  deps = []

  if (ten_enable_go_binding) {
    deps += [ ":ten_go_standalone_test_build" ]
  }
}
