#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("send_audio_frame_nodejs_app") {
  src_app = "default_app_nodejs"
  src_app_language = "nodejs"
  generated_app_src_root_dir_name = "send_audio_frame_nodejs_app"

  replace_paths_after_install_app = [
    "send_audio_frame_nodejs_app/manifest.json",
    "send_audio_frame_nodejs_app/property.json",
  ]

  replace_paths_after_install_all = [ "send_audio_frame_nodejs_app/ten_packages/extension/default_extension_nodejs/src/index.ts" ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_apps/default_app_nodejs:upload_default_app_nodejs_to_server",
      "//packages/core_extensions/default_extension_nodejs:upload_default_extension_nodejs_to_server",
      "//packages/example_extensions/simple_echo_cpp:upload_simple_echo_cpp_to_server",
      "//packages/example_extensions/simple_http_server_cpp:upload_simple_http_server_cpp_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources(
    "send_audio_frame_nodejs_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  common_files =
      exec_script("//build/ten_common/general/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }
}

group("send_audio_frame_nodejs") {
  deps = [
    ":send_audio_frame_nodejs_app",
    ":send_audio_frame_nodejs_test_files",
  ]
}
