#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("websocket_server_nodejs_app") {
  src_app = "default_app_nodejs"
  src_app_language = "nodejs"
  generated_app_src_root_dir_name = "websocket_server_nodejs_app"

  replace_paths_after_install_app = [
    "websocket_server_nodejs_app/manifest.json",
    "websocket_server_nodejs_app/property.json",
  ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_apps/default_app_nodejs:upload_default_app_nodejs_to_server",
      "//packages/example_extensions/http_server_extension_nodejs:upload_http_server_extension_nodejs_to_server",
      "//packages/example_extensions/websocket_server_nodejs:upload_websocket_server_nodejs_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources(
    "websocket_server_nodejs_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  common_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }
}

group("websocket_server_nodejs") {
  deps = [
    ":websocket_server_nodejs_app",
    ":websocket_server_nodejs_test_files",
  ]
}
