#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("go_app_websocket_server_nodejs_app") {
  src_app = "default_app_go"
  src_app_language = "go"
  generated_app_src_root_dir_name = "go_app_websocket_server_nodejs_app"

  replace_paths_after_install_app = [
    "go_app_websocket_server_nodejs_app/manifest.json",
    "go_app_websocket_server_nodejs_app/property.json",
  ]

  replace_paths_after_install_all = [ "go_app_websocket_server_nodejs_app/bin" ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//core/src/ten_runtime/binding/nodejs",
      "//packages/core_addon_loaders/nodejs_addon_loader:upload_nodejs_addon_loader_to_server",
      "//packages/core_apps/default_app_go:upload_default_app_go_to_server",
      "//packages/example_extensions/http_server_extension_nodejs:upload_http_server_extension_nodejs_to_server",
      "//packages/example_extensions/websocket_server_nodejs:upload_websocket_server_nodejs_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources(
    "go_app_websocket_server_nodejs_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }
}

group("go_app_websocket_server_nodejs") {
  deps = [
    ":go_app_websocket_server_nodejs_app",
    ":go_app_websocket_server_nodejs_test_files",
  ]
}
