#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_auxiliary_resources("standalone_vosk_asr_cpp_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_runtime:upload_ten_runtime_system_package_to_server",
      "//packages/example_extensions/vosk_asr_cpp",

      # The standalone testing for the C++ extension depends on the googletest
      # system package.
      "//third_party/googletest:googletest_system_package",
    ]
  }
}

group("standalone_vosk_asr_cpp") {
  deps = [ ":standalone_vosk_asr_cpp_files" ]
}
