#
# Copyright Â© 2024 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("hello_world_app") {
  src_app = "default_app_cpp"
  src_app_language = "cpp"
  generated_app_src_root_dir_name = "hello_world_app_source"

  replace_files_after_install_app = [
    "hello_world_app_source/manifest.json",
    "hello_world_app_source/property.json",
  ]

  replace_files_after_install_all = [
    "hello_world_app_source/ten_packages/extension/default_extension_cpp/manifest.json",
    "hello_world_app_source/ten_packages/extension/default_extension_cpp/property.json",
    "hello_world_app_source/ten_packages/extension/default_extension_cpp/src/main.cc",

    "hello_world_app_source/ten_packages/extension/default_extension_cpp/a.json",

    # The above `a.json` will be copied to the source folder, but can not be
    # copied to the build target directory with the default `BUILD.gn`. The
    # `a.json` should be added to the `sources` or `resources` in the BUILD.gn
    # of the extension.
    #
    # TODO(Liu): Provide a better way to enable users to extend the `BUILD.gn`.
    "hello_world_app_source/ten_packages/extension/default_extension_cpp/BUILD.gn",
  ]

  if (ten_enable_package_manager) {
    deps = [
      "//core/src/ten_manager",
      "//core/src/ten_runtime:upload_ten_runtime_system_package_to_server",
      "//packages/core_apps/default_app_cpp:upload_default_app_cpp_to_server",
      "//packages/core_extensions/default_extension_cpp:upload_default_extension_cpp_to_server",
      "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
    ]
  }
}

ten_package_test_prepare_client("hello_world_app_client") {
  sources = [ "client/client.cc" ]
  include_dirs = [
    "//core/src",
    "//core",
    "//packages",
    "//tests",
  ]
  deps = [
    "//core/src/ten_runtime",
    "//packages/core_protocols/msgpack:msgpack_files",
    "//tests/common/client:msgpack_client",
    "//third_party/msgpack:msgpackc",
    "//third_party/nlohmann_json",
  ]
}

ten_package_test_prepare_auxiliary_resources("hello_world_app_test_files") {
  resources = [
    "//tests/ten_runtime/integration/common=>common",
    "__init__.py",
    "test_case.py",
  ]
  if (enable_sanitizer && !is_clang) {
    resources += [ "//tests/ten_runtime/integration/tools/use_asan_lib_marker=>use_asan_lib_marker" ]
  }
}

group("hello_world") {
  deps = [
    ":hello_world_app",
    ":hello_world_app_client",
    ":hello_world_app_test_files",
  ]
}
