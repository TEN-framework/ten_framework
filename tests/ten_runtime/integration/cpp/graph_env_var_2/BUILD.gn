#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("graph_env_var_2_app") {
  src_app = "default_app_cpp"
  src_app_language = "cpp"
  generated_app_src_root_dir_name = "graph_env_var_2_app"

  replace_paths_after_install_app = [
    "graph_env_var_2_app/manifest.json",
    "graph_env_var_2_app/property.json",
  ]

  replace_paths_after_install_all = [
    "graph_env_var_2_app/ten_packages/extension/default_extension_cpp/manifest.json",
    "graph_env_var_2_app/ten_packages/extension/default_extension_cpp/property.json",
    "graph_env_var_2_app/ten_packages/extension/default_extension_cpp/src/main.cc",
    "graph_env_var_2_app/ten_packages/extension/default_extension_cpp/BUILD.gn",
  ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//core/src/ten_runtime:upload_ten_runtime_system_package_to_server",
      "//packages/core_apps/default_app_cpp:upload_default_app_cpp_to_server",
      "//packages/core_extensions/default_extension_cpp:upload_default_extension_cpp_to_server",
      "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
    ]
  }
}

ten_package_test_prepare_client("graph_env_var_2_app_client") {
  sources = [ "client/client.cc" ]
  include_dirs = [
    "//core/src",
    "//core",
    "//packages",
    "//tests",
  ]
  deps = [
    "//core/src/ten_runtime",
    "//packages/core_protocols/msgpack:msgpack_files",
    "//tests/common/client:msgpack_client",
    "//third_party/msgpack:msgpackc",
    "//third_party/nlohmann_json",
  ]
}

ten_package_test_prepare_auxiliary_resources("graph_env_var_2_app_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }
}

group("graph_env_var_2") {
  deps = [
    ":graph_env_var_2_app",
    ":graph_env_var_2_app_client",
    ":graph_env_var_2_app_test_files",
  ]
}
