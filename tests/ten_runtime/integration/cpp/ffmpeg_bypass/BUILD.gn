#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("ffmpeg_bypass_app") {
  src_app = "default_app_cpp"
  src_app_language = "cpp"
  generated_app_src_root_dir_name = "ffmpeg_bypass_app"

  replace_paths_after_install_app = [
    "ffmpeg_bypass_app/manifest.json",
    "ffmpeg_bypass_app/property.json",
  ]

  replace_paths_after_install_all = [
    "ffmpeg_bypass_app/ten_packages/extension/ffmpeg_client/src/main.cc",
    "ffmpeg_bypass_app/ten_packages/extension/ffmpeg_client/manifest.json",
  ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_apps/default_app_cpp:upload_default_app_cpp_to_server",
      "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
      "//packages/example_extensions/ffmpeg_client:upload_ffmpeg_client_to_server",
      "//packages/example_extensions/ffmpeg_demuxer:upload_ffmpeg_demuxer_to_server",
      "//packages/example_extensions/ffmpeg_muxer:upload_ffmpeg_muxer_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources("ffmpeg_bypass_app_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
    "video_cmp.py",
  ]

  common_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }
}

group("ffmpeg_bypass") {
  deps = [
    ":ffmpeg_bypass_app",
    ":ffmpeg_bypass_app_test_files",
  ]
}
