#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("go_app_cythonize_app") {
  src_app = "default_app_go"
  src_app_language = "go"
  generated_app_src_root_dir_name = "go_app_cythonize_app"

  replace_paths_after_install_app = [
    "go_app_cythonize_app/manifest.json",
    "go_app_cythonize_app/property.json",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/__init__.pyx",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/addon.pyx",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/extension.pyx",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/log.pyx",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/manifest.json",
    "go_app_cythonize_app/ten_packages/extension/default_extension_python/property.json",
  ]

  replace_paths_after_install_all = [ "go_app_cythonize_app/bin/bootstrap" ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_addon_loaders/python_addon_loader:upload_python_addon_loader_to_server",
      "//packages/core_apps/default_app_go:upload_default_app_go_to_server",
      "//packages/core_extensions/default_extension_go:upload_default_extension_go_to_server",
      "//packages/core_extensions/default_extension_python:upload_default_extension_python_to_server",
      "//packages/example_extensions/pil_demo_python:upload_pil_demo_python_to_server",
      "//packages/example_extensions/simple_echo_cpp:upload_simple_echo_cpp_to_server",
      "//packages/example_extensions/simple_http_server_cpp:upload_simple_http_server_cpp_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources("go_app_cythonize_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }
}

group("go_app_cythonize") {
  deps = [
    ":go_app_cythonize_app",
    ":go_app_cythonize_test_files",
  ]
}
