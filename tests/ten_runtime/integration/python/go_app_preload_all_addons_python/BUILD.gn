#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("go_app_preload_all_addons_python_app") {
  src_app = "default_app_go"
  src_app_language = "go"
  generated_app_src_root_dir_name = "go_app_preload_all_addons_python_app"

  replace_paths_after_install_app = [
    "go_app_preload_all_addons_python_app/manifest.json",
    "go_app_preload_all_addons_python_app/property.json",
  ]

  replace_paths_after_install_all = [
    "go_app_preload_all_addons_python_app/ten_packages/extension/default_async_extension_python/extension.py",
    "go_app_preload_all_addons_python_app/ten_packages/extension/default_async_extension_python/addon.py",
    "go_app_preload_all_addons_python_app/bin/bootstrap",
  ]

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_addon_loaders/python_addon_loader:upload_python_addon_loader_to_server",
      "//packages/core_apps/default_app_go:upload_default_app_go_to_server",
      "//packages/core_extensions/default_async_extension_python:upload_default_async_extension_python_to_server",
      "//packages/core_extensions/default_extension_go:upload_default_extension_go_to_server",
      "//packages/example_extensions/simple_echo_cpp:upload_simple_echo_cpp_to_server",
      "//packages/example_extensions/simple_http_server_cpp:upload_simple_http_server_cpp_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources("go_app_preload_all_addons_python_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  common_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }
}

group("go_app_preload_all_addons_python") {
  deps = [
    ":go_app_preload_all_addons_python_app",
    ":go_app_preload_all_addons_python_test_files",
  ]
}
