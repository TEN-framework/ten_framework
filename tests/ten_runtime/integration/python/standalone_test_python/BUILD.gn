#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_auxiliary_resources(
    "standalone_test_python_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  common_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }

  ext_files = exec_script(
          "//.gnfiles/build/scripts/glob_file.py",
          [
            "--dir",
            rebase_path(
                "//tests/ten_runtime/integration/python/standalone_test_python/default_extension_python/**/*"),
            "--dir-base",
            rebase_path(
                "//tests/ten_runtime/integration/python/standalone_test_python/default_extension_python"),
            "--recursive",
            "--only-output-file",
          ],
          "json")

  foreach(ext_file, ext_files) {
    ext_file_rel_path = ext_file.relative_path
    resources += [ "//tests/ten_runtime/integration/python/standalone_test_python/default_extension_python/${ext_file_rel_path}=>default_extension_python/${ext_file_rel_path}" ]
  }
}

group("standalone_test_python") {
  deps = [
    ":standalone_test_python_test_files",
    "//tests/ten_runtime/integration/python:copy_pytest_ini_for_ten_runtime_python_integration_test",
  ]
}
