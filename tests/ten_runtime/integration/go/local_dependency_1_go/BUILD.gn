#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("local_dependency_1_go_app") {
  src_app = "default_app_go"
  src_app_language = "go"
  generated_app_src_root_dir_name = "local_dependency_1_go_app"

  replace_paths_after_install_app = [
    "local_dependency_1_go_app/manifest.json",
    "local_dependency_1_go_app/property.json",
    "local_dependency_1_go_app/ten_packages/extension/extension_a/extension.go",
    "local_dependency_1_go_app/ten_packages/extension/extension_a/go.mod",
    "local_dependency_1_go_app/ten_packages/extension/extension_a/manifest.json",
    "local_dependency_1_go_app/ten_packages/extension/extension_a/property.json",
  ]

  if (ten_enable_go_app_leak_check) {
    replace_paths_after_install_app += [ "local_dependency_1_go_app/main.go" ]
  }

  deps = [
    "//core/src/ten_manager",
    "//packages/core_apps/default_app_go:upload_default_app_go_to_server",
    "//packages/core_extensions/default_extension_go:upload_default_extension_go_to_server",
    "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
  ]
}

ten_package_test_prepare_client("local_dependency_1_go_app_client") {
  sources = [ "client/client.cc" ]
  include_dirs = [
    "//core/src",
    "//core",
    "//packages",
    "//tests",
  ]
  deps = [
    "//core/src/ten_runtime",
    "//packages/core_protocols/msgpack:msgpack_files",
    "//tests/common/client:msgpack_client",
    "//third_party/msgpack:msgpackc",
    "//third_party/nlohmann_json",
  ]
}

ten_package_test_prepare_auxiliary_resources(
    "local_dependency_1_go_app_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }
}

group("local_dependency_1_go") {
  deps = [
    ":local_dependency_1_go_app",
    ":local_dependency_1_go_app_client",
    ":local_dependency_1_go_app_test_files",
  ]
}
