#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("handle_error_go_app") {
  src_app = "default_app_go"
  src_app_language = "go"
  generated_app_src_root_dir_name = "handle_error_go_app"

  replace_paths_after_install_app = [
    "handle_error_go_app/manifest.json",
    "handle_error_go_app/property.json",
    "handle_error_go_app/ten_packages/extension/extension_a/extension.go",
    "handle_error_go_app/ten_packages/extension/extension_a/go.mod",
    "handle_error_go_app/ten_packages/extension/extension_a/manifest.json",
    "handle_error_go_app/ten_packages/extension/extension_a/property.json",
    "handle_error_go_app/ten_packages/extension/extension_b/extension.go",
    "handle_error_go_app/ten_packages/extension/extension_b/go.mod",
    "handle_error_go_app/ten_packages/extension/extension_b/manifest.json",
    "handle_error_go_app/ten_packages/extension/extension_b/property.json",
  ]

  if (ten_enable_go_app_leak_check) {
    replace_paths_after_install_app += [ "handle_error_go_app/main.go" ]
  }

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//packages/core_apps/default_app_go:upload_default_app_go_to_server",
      "//packages/core_extensions/default_extension_go:upload_default_extension_go_to_server",
      "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
    ]
  }
}

ten_package_test_prepare_client("handle_error_go_app_client") {
  sources = [ "client/client.cc" ]
  include_dirs = [
    "//core/src",
    "//core",
    "//packages",
    "//tests",
  ]
  deps = [
    "//core/src/ten_runtime",
    "//packages/core_protocols/msgpack:msgpack_files",
    "//tests/common/client:msgpack_client",
    "//third_party/msgpack:msgpackc",
    "//third_party/nlohmann_json",
  ]
}

ten_package_test_prepare_auxiliary_resources("handle_error_go_app_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  common_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//tests/ten_runtime/integration/common/**/*"),
                    "--dir-base",
                    rebase_path("//tests/ten_runtime/integration/common"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(common_file, common_files) {
    common_file_rel_path = common_file.relative_path
    resources += [ "//tests/ten_runtime/integration/common/${common_file_rel_path}=>common/${common_file_rel_path}" ]
  }
}

group("handle_error_go") {
  deps = [
    ":handle_error_go_app",
    ":handle_error_go_app_client",
    ":handle_error_go_app_test_files",
  ]
}
